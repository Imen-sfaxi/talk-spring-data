buildscript {
    dependencies {
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }
}

plugins {
    id 'org.ysb33r.vfs' version '1.0'
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'com.github.jruby-gradle.base' version '1.2.1'
}

apply plugin: 'java'

version = '1.0.0-SNAPSHOT'

ext {
    revealjsVersion = '3.3.0'
    asciidoctorBackendVersion = '1.0.2'
    downloadDir = new File(buildDir,'download')
    templateDir = new File(downloadDir,'templates')
    revealjsDir   = new File(downloadDir,'reveal.js')
}

repositories {
    jcenter()
}

dependencies {
    gems 'rubygems:slim:3.0.6'
    gems 'rubygems:thread_safe:0.3.5'
}

task download {
    doLast {
        mkdir downloadDir
        vfs {
            cp "zip:https://github.com/asciidoctor/asciidoctor-reveal.js/archive/v${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
                    templateDir, recursive: true, overwrite: false
            cp "zip:https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
                    revealjsDir, recursive: true, overwrite: false
        }
    }
}

download {
    description 'Download extra revealjs resources'
    outputs.dir templateDir
    outputs.dir revealjsDir
}

asciidoctorj {
    version = '1.5.4'
}

asciidoctor {
    dependsOn jrubyPrepare, download
    sources {
        include 'index.adoc'
    }
    resources {
        from (sourceDir) {
            include 'images/**'
        }
        from (sourceDir) {
            include 'themes/**'
        }
        from (downloadDir) {
            include 'reveal.js/**'
        }
    }
    backends 'revealjs'
    attributes \
        'build-gradle': file('build.gradle'),
        'sourcedir': project.sourceSets.main.java.srcDirs[0],
        'endpoint-url': 'http://example.org',
        'source-highlighter': 'coderay',
        'imagesdir': './images',
        'toc': 'left',
        'icons': 'font',
        'setanchors': '',
        'docinfo1': '',
        'revealjs_customtheme': 'themes/ekino.css',
        'revealjs_transition': 'linear',
        'revealjs_history': 'true',
        'revealjs_slideNumber': 'true'
    options template_dirs: [new File(sourceDir,'templates/ekino').absolutePath, new File(templateDir,'templates/slim').absolutePath]
}
